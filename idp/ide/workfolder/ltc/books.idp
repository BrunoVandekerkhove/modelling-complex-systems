vocabulary V {
    type Time isa nat
    partial Next(Time) : Time
    Start : Time

    type Person
    type Book

    owns(Time, Person, Book)
    I_owns(Person, Book) // Initial predicate for owns
    C_owns(Time, Person, Book)
    Cn_owns(Time, Person, Book)

    give(Time, Person, Book, Person)
}

structure S : V {
    Time = {0..5}
    Person = { Bob; John; Mary; }
    Book = { B1; B2; B3; }

    // Initially
    I_owns = { Bob,B1; John,B2; Mary,B3; }
}

theory timeTheo : V {
    {
        Start = MIN[:Time].
        ! t: Next(t) = t + 1 <- Time(t + 1).
    }
}

theory T : V {
    
    {
        // Successor State Action Axiom & Inertia
        ! p[Person] b[Book]: owns(Start, p, b) <- I_owns(p, b).
        ! t[Time] p[Person] b[Book]: owns(Next(t), p, b) <- C_owns(t, p, b).
        ! t[Time] p[Person] b[Book]: owns(Next(t), p, b) <- owns(t, p, b) & ~Cn_owns(t, p, b).
    }

    {
        // Expressing causations
        ! t[Time] p[Person] b[Book]: C_owns(t, p, b) <- ?g: give(t,g,b,p).
        ! t[Time] p[Person] b[Book]: Cn_owns(t, p, b) <- ?r: give(t,p,b,r).
    }
    
    // Concurrency
    !t: #{g,b,r: give(t,g,b,r)} =< 1.

    // Preconditions
    // - A person needs to own a book if he wants to give it away
	!t,g,b,r: give(t,g,b,r) => owns(t,g,b).
    // - A book can be given to only one person
	!t,g,b,r1,r2: give(t,g,b,r1) & give(t,g,b,r2) => r1 = r2.
    // - A person cannot give a book to himself
    !t,g,b,r: give(t,g,b,r) => g ~= r.

}

procedure findmodels() {
    local timeExpanded = calculatedefinitions(timeTheo, S)
    stdoptions.nbmodels = 5
    printmodels(modelexpand(T, timeExpanded))
}



procedure simulate() {
    stdoptions.nbmodels = 5
    print("----> Starting interactive simulation")
    local states = initialise(T, S)

    // use one of those initial states to continue with (again and again and ...)
    while true do
        printmodels(states)
        print("Please enter which state to use")
        local num = tonumber(io.read("*line"))
        if num == nil then
            break
        elseif num < 1 or num > 5 then
            break
        end
        local chosen = states[num]
        states = progress(T, chosen)
    end
}



procedure main() {
    // This method lets you walk through your solutions interactively
    simulate()
    // This method (defined above) simply searches models of your theory
    // findmodels()
}